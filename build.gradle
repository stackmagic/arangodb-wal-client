
// repositories and dependencies for buildscript itself
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'net.swisstech:swissarmyknife:+'
	}
}

plugins {
	id 'net.swisstech.eclipseenhancer' version '1.0.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'

description = 'Client for the ArangoDB Write-Ahead-Log (WAL) over HTTP'
group       = 'net.swisstech'
version     = '0.0.1'

// re-use repositories from buildscript
buildscript.repositories.each { repositories.add(it) }

// java version
sourceCompatibility = 1.7
targetCompatibility = 1.7

// task to generate wrapper
task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}

// extra configuration for integration tests
configurations {
	intTestCompile
}

sourceSets {
	intTest {
		compileClasspath = sourceSets.main.output + configurations.testRuntime
		runtimeClasspath = sourceSets.main.output + configurations.testRuntime + output
	}
}

// project dependencies
dependencies {
	compile 'com.fasterxml.jackson.core:jackson-databind:+'
	compile 'com.squareup.okhttp:okhttp:+'
	compile 'commons-io:commons-io:+'
	compile 'net.swisstech:swissarmyknife:+'
	compile 'org.slf4j:slf4j-api:+'

	testCompile 'ch.qos.logback:logback-classic:+'
	testCompile 'org.testng:testng:+'
	testCompile 'org.mockito:mockito-all:+'

	intTestCompile 'ch.qos.logback:logback-classic:+'
	intTestCompile 'org.testng:testng:+'
	intTestCompile 'org.mockito:mockito-all:+'
}

// we no likey log4j
configurations {
	all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

// normal unit tests
test {
	useTestNG()
}

// setup for integration tests, requires a local installation of arangodb
ext.arangoDbPort = new Integer(18529)
ext.arangoDataDir = "/tmp/arangodb_wal_client_tests_${System.currentTimeMillis()}"

task('intTest', type: Test, dependsOn: 'intTestClasses') {
	useTestNG()
	testLogging.showStandardStreams = true
	group          = 'verification'
	testSrcDirs    = sourceSets.intTest.java.srcDirs as List
	testClassesDir = sourceSets.intTest.output.classesDir
	classpath      = sourceSets.intTest.runtimeClasspath
	systemProperty 'ARANGODB_PORT', project.arangoDbPort
}

afterEvaluate {

	task('startArangoDb') << {
		new File(project.arangoDataDir + "/data").mkdirs()
		project.ext.arangodb_process = net.swisstech.swissarmyknife.sys.linux.BackgroundProcess
			.launch([
				'/usr/sbin/arangod',
				'--server.endpoint'   , "tcp://127.0.0.1:${project.arangoDbPort}",
				'--database.directory', "${project.arangoDataDir}/data",
				'--log.file'          , '+'
			], null)
			.waitForOpenPorts([ project.arangoDbPort ], 10000)
	}

	task('stopArangoDb') << {
		project.arangodb_process.shutdown()
		project.delete(project.arangoDataDir)
	}

	tasks.intTest.dependsOn('startArangoDb')
	tasks.intTest.finalizedBy('stopArangoDb')
}
