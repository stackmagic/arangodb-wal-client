
// repositories and dependencies for buildscript itself
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'net.swisstech:swissarmyknife:+'
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'maven-publish'
	id 'project-report'
	id 'com.jfrog.bintray'             version '1.1'
	id 'net.swisstech.eclipseenhancer' version '1.0.0'
}

description = 'Client for the ArangoDB Write-Ahead-Log (WAL) over HTTP'
group       = 'net.swisstech'
version     = '1.0.0'

// re-use repositories from buildscript
buildscript.repositories.each { repositories.add(it) }

// java version
sourceCompatibility = 1.7
targetCompatibility = 1.7

// task to generate wrapper
task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}

// source-code jar
task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

// bintray uses the publication, also includes source jar
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar {
				classifier 'sources'
			}
		}
	}
}

def getProperty = { String name ->
	try {
		return project[name]
	}
	catch (MissingPropertyException e) {
		println "!!! property ${name} is not available"
		return ""
	}
}

bintray {
	// key and user must be in your ~/.gradle/gradle.properties
	key          = getProperty('swisstech_bintray_apikey')
	user         = getProperty('swisstech_bintray_user')
	dryRun       = false
	publish      = true
	publications = [ 'mavenJava' ]
	pkg {
		repo            = 'maven'
		name            = project.name
		desc            = project.description
		licenses        = [ 'Apache-2.0' ]
		websiteUrl      = "https://github.com/stackmagic/${project.name}"
		vcsUrl          = "https://github.com/stackmagic/${project.name}.git"
		issueTrackerUrl = "https://github.com/stackmagic/${project.name}/issues"
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = project.description
		}
	}
}

// extra configuration for integration tests
configurations {
	intTestCompile
}

sourceSets {
	intTest {
		compileClasspath = sourceSets.main.output + configurations.testRuntime
		runtimeClasspath = sourceSets.main.output + configurations.testRuntime + output
	}
}

// project dependencies
ext.versions = [
	  jackson        : '2.5.0'
	, okhttp         : '2.2.0'
	, commons_io     : '2.4'
	, swissarmyknife : '1.1.8'
	, logback_classic: '1.1.2'
	, testng         : '6.8.17'
	, mockito        : '1.10.19'
]

dependencies {
	compile        "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
	compile        "com.squareup.okhttp:okhttp:${versions.okhttp}"
	compile        "commons-io:commons-io:${versions.commons_io}"
	compile        "net.swisstech:swissarmyknife:${versions.swissarmyknife}"

	testCompile    "ch.qos.logback:logback-classic:${versions.logback_classic}"
	testCompile    "org.testng:testng:${versions.testng}"
	testCompile    "org.mockito:mockito-all:${versions.mockito}"

	intTestCompile "ch.qos.logback:logback-classic:${versions.logback_classic}"
	intTestCompile "org.testng:testng:${versions.testng}"
	intTestCompile "org.mockito:mockito-all:${versions.mockito}"
}

// normal unit tests
test {
	useTestNG()
}

// setup for integration tests, requires a local installation of arangodb
ext.arangoDbPort = new Integer(18529)
ext.arangoDataDir = "/tmp/arangodb_wal_client_tests_${System.currentTimeMillis()}"

task('intTest', type: Test, dependsOn: 'intTestClasses') {
	useTestNG()
	testLogging.showStandardStreams = true
	group          = 'verification'
	testSrcDirs    = sourceSets.intTest.java.srcDirs as List
	testClassesDir = sourceSets.intTest.output.classesDir
	classpath      = sourceSets.intTest.runtimeClasspath
	systemProperty 'ARANGODB_PORT', project.arangoDbPort
}

afterEvaluate {

	task('startArangoDb') << {
		new File(project.arangoDataDir + "/data").mkdirs()
		project.ext.arangodb_process = net.swisstech.swissarmyknife.sys.linux.BackgroundProcess
			.launch([
				'/usr/sbin/arangod',
				'--server.endpoint'   , "tcp://127.0.0.1:${project.arangoDbPort}",
				'--database.directory', "${project.arangoDataDir}/data",
				'--log.file'          , '+'
			], null)
			.waitForOpenPorts([ project.arangoDbPort ], 10000)
	}

	task('stopArangoDb') << {
		project.arangodb_process.shutdown()
		project.delete(project.arangoDataDir)
	}

	tasks.intTest.dependsOn('startArangoDb')
	tasks.intTest.finalizedBy('stopArangoDb')
}
